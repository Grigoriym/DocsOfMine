apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "kotlin-kapt"
apply plugin: "dagger.hilt.android.plugin"

android {
    compileSdk ProjectConfig.compileSdk

    signingConfigs {
        debug {

        }
        release {
            storeFile file(DOCS_OF_MINE_STORE_FILE)
            storePassword DOCS_OF_MINE_STORE_PASSWORD
            keyAlias DOCS_OF_MINE_STORE_ALIAS
            keyPassword DOCS_OF_MINE_STORE_ALIAS_PASSWORD
        }
    }

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdk ProjectConfig.minSdk
        targetSdk ProjectConfig.targetSdk
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18

        coreLibraryDesugaringEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    kapt {
        correctErrorTypes true

        javacOptions {
            option("-Xmaxerrs", 500)
        }
        arguments{
            arg("dagger.formatGeneratedSource", "enabled")
            arg("dagger.fullBindingGraphValidation", "ERROR")
        }
    }
    kotlinOptions {
        jvmTarget = '18'

        freeCompilerArgs += "-opt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-opt-in=kotlin.contracts.ExperimentalContracts"
        freeCompilerArgs += "-opt-in=kotlinx.serialization.ExperimentalSerializationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.material.ExperimentalMaterialApi"
    }
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    implementation("com.google.dagger:hilt-android:${versions.google.hilt}")
    kapt("com.google.dagger:hilt-android-compiler:${versions.google.hilt}")

    implementation("com.jakewharton.timber:timber:${versions.timber}")
}